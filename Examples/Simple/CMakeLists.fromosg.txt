
IF(OSG_OGL_COREONLY AND NOT OSG_CHECK_COREONLY)
  RETURN()
ENDIF()

PROJECT(OpenSGSimpleExamples)

MESSAGE(STATUS "Building simple examples integrated")

#############################################################################
# find sources

FILE(GLOB SRCFILES "*.cpp")

# stuff that is broken -- these examples need to be fixed
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/clusterclientshader.cpp")
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/geometryshader.cpp")

#############################################################################
# get dependencies

SET(OSG_COMPONENTS OSGBase
                   OSGCluster
                   OSGDrawable
                   OSGFileIO
                   OSGGroup
                   OSGEffectGroups
                   OSGImageFileIO
                   OSGState
                   OSGSystem
                   OSGUtil
                   OSGWindow
                   OSGWindowGLUT)

IF(UNIX)
    IF(APPLE)
        LIST(APPEND OSG_COMPONENTS OSGWindowCocoa)
    ELSE(APPLE)
        LIST(APPEND OSG_COMPONENTS OSGWindowX)
    ENDIF(APPLE)
ENDIF(UNIX)

IF(WIN32)
    LIST(APPEND OSG_COMPONENTS OSGWindowWIN32)
ENDIF(WIN32)

SET(DEP_INCLUDE_DIRS)
SET(DEP_LIBRARY_DIRS)
SET(DEP_LIBRARIES)

SET(OSG_EXTERNAL_COMPONENTS Boost GLUT)

#############
#### X11 (optional)

IF(X11_FOUND AND NOT APPLE)
    LIST(APPEND OSG_EXTERNAL_COMPONENTS X11)
    LIST(APPEND OSG_COMPONENTS OSGWindowX)
ELSE(X11_FOUND AND NOT APPLE)
    LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/clusterserverx.cpp")
ENDIF(X11_FOUND AND NOT APPLE)

#############
#### OpenSG

FOREACH(OSGDEP ${OSG_COMPONENTS})
    IF(NOT EXISTS "${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")
        MESSAGE(STATUS "Dependency (${OSGDEP}) not found, "
                       "can not build ${PROJECT_NAME}\n")
        RETURN()
    ENDIF(NOT EXISTS "${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")

    INCLUDE("${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")
    INCLUDE_DIRECTORIES(${${OSGDEP}_INC})
ENDFOREACH()

FOREACH(OSGEXTDEP ${OSG_EXTERNAL_COMPONENTS})
    IF(NOT ${OSGEXTDEP}_FOUND)
        MESSAGE(FATAL_ERROR "Dependency (${OSGEXTDEP}) not found, "
                            "can not build ${PROJECT_NAME}\n")
        RETURN()
    ENDIF()

    INCLUDE_DIRECTORIES(SYSTEM ${${OSGEXTDEP}_INCLUDE_DIR})
ENDFOREACH()

set(OSG_ADDON_LIBRARIES "")

IF(ANTTWEAKBAR_FOUND)
    INCLUDE_DIRECTORIES(AFTER ${ANTTWEAKBAR_INCLUDE_DIR})
    LIST(APPEND OSG_ADDON_LIBRARIES ${ANTTWEAKBAR_LIBRARIES})
    add_definitions(-DOSG_WITH_ANTTWEAKBAR)
ENDIF()

#############################################################################
# build executables

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})

FOREACH(GLOBAL_INC ${OSG_GLOBAL_DEP_INCDIR})
  INCLUDE_DIRECTORIES(${${GLOBAL_INC}})
ENDFOREACH()

FOREACH(SRC ${SRCFILES})

    GET_FILENAME_COMPONENT(EXE ${SRC} NAME_WE)

    ADD_EXECUTABLE(${EXE} ${OSGEXCLUDE_EXAMPLES_SIMPLE} 
                          ${SRC} ${OSG_ADD_GLOBAL_EXE_SRC})

    ADD_DEPENDENCIES(OSGAllSimpleExamples ${EXE})

    FOREACH(OSGDEP ${OSG_COMPONENTS})
        ADD_DEPENDENCIES(${EXE} ${OSGDEP})

        TARGET_LINK_LIBRARIES(${EXE} ${OSGDEP})
    ENDFOREACH()

    TARGET_LINK_LIBRARIES(${EXE} ${OSG_ADDON_LIBRARIES})

    IF(LINUX AND CMAKE_BUILD_TYPE STREQUAL "DebugGV" AND OSG_ADD_CXX_FLAGS_GV)
        SET_PROPERTY(TARGET ${EXE}
                     APPEND PROPERTY COMPILE_FLAGS ${OSG_ADD_CXX_FLAGS_GV})
    ENDIF()
    
    IF(WIN32)
      SET_PROPERTY(TARGET ${EXE} PROPERTY FOLDER "Examples/Simple")
    ENDIF()
    
    #Install
    IF(OSG_INSTALL_EXAMPLES_EXE)
      IF(WIN32)
        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS Release
                RUNTIME DESTINATION share/OpenSG/Examples/Simple/bin/rel)
        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS Debug
                RUNTIME DESTINATION share/OpenSG/Examples/Simple/bin/debug)
        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS ReleaseNoOpt
                RUNTIME DESTINATION share/OpenSG/Examples/Simple/bin/relnoopt)
        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS DebugOpt
                RUNTIME DESTINATION share/OpenSG/Examples/Simple/bin/debugopt)
      ELSE()
        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS Release
                DESTINATION share/OpenSG/Examples/Simple/bin
                COMPONENT examples_basic)

        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS Debug
                DESTINATION share/OpenSG/Examples/Simple/bin/debug
                COMPONENT examples_basic)

        INSTALL(TARGETS ${EXE}
                CONFIGURATIONS DebugGV
                DESTINATION share/OpenSG/Examples/Simple/bin/debug
                COMPONENT examples_basic)
      ENDIF()
    ENDIF()
    IF(OSG_INSTALL_EXAMPLES)
        INSTALL(FILES "${SRC}"
                DESTINATION share/OpenSG/Examples/Simple
                COMPONENT examples_basic)
    ENDIF()
ENDFOREACH(SRC)
    

IF(OSG_INSTALL_EXAMPLES)
    INSTALL(FILES CMakeLists.standalone.txt
            DESTINATION share/OpenSG/Examples/Simple
            RENAME CMakeLists.txt
            COMPONENT examples_basic)
ENDIF()

