
PROJECT(OpenSGSimpleExamples)

MESSAGE(STATUS "Building simple examples integrated")

#############################################################################
# find sources

FILE(GLOB SRCFILES "*.cpp")

# stuff that is broken -- these examples need to be fixed
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/clusterclientshader.cpp")
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/geometryshader.cpp")
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/phongshaderwithglmaterials.cpp")
LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/shader.cpp")

#############################################################################
# get dependencies

SET(OSG_COMPONENTS OSGBase OSGCluster OSGDrawable OSGFileIO OSGGroup OSGState
                   OSGSystem OSGUtil OSGWindow OSGWindowGLUT)

IF(UNIX)
    IF(APPLE)
        LIST(APPEND OSG_COMPONENTS OSGWindowCocoa)
    ELSE(APPLE)
        LIST(APPEND OSG_COMPONENTS OSGWindowX)
    ENDIF(APPLE)
ENDIF(UNIX)

IF(WIN32)
    LIST(APPEND OSG_COMPONENTS OSGWindowWIN32)
ENDIF(WIN32)

SET(DEP_INCLUDE_DIRS)
SET(DEP_LIBRARY_DIRS)
SET(DEP_LIBRARIES)

SET(OSG_EXTERNAL_COMPONENTS Boost GLUT)

#############
#### X11 (optional)

IF(X11_FOUND AND NOT APPLE)
    LIST(APPEND OSG_EXTERNAL_COMPONENTS X11)
    LIST(APPEND OSG_COMPONENTS OSGWindowX)
ELSE(X11_FOUND AND NOT APPLE)
    LIST(REMOVE_ITEM SRCFILES "${PROJECT_SOURCE_DIR}/clusterserverx.cpp")
ENDIF(X11_FOUND AND NOT APPLE)

#############
#### OpenSG

FOREACH(OSGDEP ${OSG_COMPONENTS})
    IF(NOT EXISTS "${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")
        MESSAGE(STATUS "Dependency (${OSGDEP}) not found, "
                       "can not build ${PROJECT_NAME}\n")
        RETURN()
    ENDIF(NOT EXISTS "${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")

    INCLUDE("${CMAKE_BINARY_DIR}/${OSGDEP}.cmake")
    INCLUDE_DIRECTORIES(${${OSGDEP}_INC})
ENDFOREACH()

FOREACH(OSGEXTDEP ${OSG_EXTERNAL_COMPONENTS})
    IF(NOT ${OSGEXTDEP}_FOUND)
        MESSAGE(FATAL_ERROR "Dependency (${OSGEXTDEP}) not found, "
                            "can not build ${PROJECT_NAME}\n")
        RETURN()
    ENDIF()

    INCLUDE_DIRECTORIES(${${OSGEXTDEP}_INCLUDE_DIR})
ENDFOREACH()


#############################################################################
# build executables

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})

FOREACH(SRC ${SRCFILES})

    GET_FILENAME_COMPONENT(EXE ${SRC} NAME_WE)

    ADD_EXECUTABLE(${EXE} ${OSGEXCLUDE_EXAMPLES_SIMPLE} ${SRC})

    ADD_DEPENDENCIES(OSGAllSimpleExamples ${EXE})

    FOREACH(OSGDEP ${OSG_COMPONENTS})
        ADD_DEPENDENCIES(${EXE} ${OSGDEP})

        TARGET_LINK_LIBRARIES(${EXE} ${OSGDEP})
    ENDFOREACH()

    IF(LINUX AND CMAKE_BUILD_TYPE STREQUAL "DebugGV" AND OSG_ADD_CXX_FLAGS_GV)
        SET_PROPERTY(TARGET ${EXE}
                     APPEND PROPERTY COMPILE_FLAGS ${OSG_ADD_CXX_FLAGS_GV})
    ENDIF()
    
    
    #Install
    IF(OSG_INSTALL_EXAMPLES)
        INSTALL(TARGETS ${EXE}
                DESTINATION share/OpenSG/Examples/Simple
                COMPONENT examples_basic)
        INSTALL(FILES "${SRC}"
                DESTINATION share/OpenSG/Examples/Simple
                COMPONENT examples_basic)
    ENDIF()
ENDFOREACH(SRC)
    

IF(OSG_INSTALL_EXAMPLES)
    INSTALL(DIRECTORY "${CMAKE_SOURCE_DIR}/Examples/Tutorial/Data"
            DESTINATION share/OpenSG/Examples/Simple
            COMPONENT examples_basic)
ENDIF()

