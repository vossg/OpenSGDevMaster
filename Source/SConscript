#!python
import os, os.path
pj = os.path.join
from LibraryUtils import *

Import('*')

common_lib_env = build_env.Copy()

if verbose_build:
   print "Setting up build for libraries: ",
   
for (lib_name, library) in lib_map.iteritems():
   if verbose_build:
      print "Library: ", lib_name, "  tests:", library.test_files, "  unittests:", library.unittest_files
   else:
      print "%s"%lib_name,
   
   # ---- Setup and Build library --- #
   # - Setup environment for build: library deps, custom options
   # - Set builder for the library
   # - Install headers if in first pass
   lib_env = common_lib_env.Copy()
   full_lib_name = lib_name + shared_lib_suffix

   # Use ConfigInfo to find build flags for building with/against this library and it's dependencies
   # - We have to pull our library name off the list (slight hack)   
   default_lib_settings = LibraryInfo()
   config_info = ConfigInfoAdapter(lib_name, lib_map, default_lib_settings,
                                   osg_lib_suffix=shared_lib_suffix)
   dep_libs = [l for l in config_info.getLibs() if l != full_lib_name]
   lib_env.Append(CPPPATH=config_info.getIncPath(),
                  LIBPATH=[inst_paths["lib_inst_combo"]] + config_info.getLibPath(),
                  LIBS=dep_libs)

   # XXX: Apply custom options
   #
   # Add the define needed for declspec to know what library we are building
   compile_define = "OSG_COMPILE%sLIB"%lib_name[3:].upper()
   lib_env.Append(CPPDEFINES=[compile_define])
    
   shared_lib = lib_env.SharedLibrary(full_lib_name, library.source_files)
   shared_lib_inst = lib_env.Install(inst_paths['lib_inst_combo'], shared_lib)
      
   # If default combo type, then make links from lib dir
   if default_combo_type == combo["type"] and (GetPlatform() != "win32"):
      lib_env.Install(inst_paths["lib"], shared_lib_inst)      
   
   # Install headers (but only do it once during variant_pass 0 )
   if variant_pass == 0:      
      lib_env.Install(pj(inst_paths["include"],"OpenSG"), library.header_files)
   
   # ---- Build Tests ---- #   
   # Verify we have all the deps for the tests
   #   todo: How do we verify the "other test libs"
   have_test_deps = True
   for n in library.osg_test_libs:
      if not lib_map.has_key(n):
         have_test_deps = False
         print "   missing test dep library:", n
   
   if have_test_deps:
      # Use ConfigInfo to find build flags for building applications against this library
      config_info = ConfigInfoAdapter([lib_name] + library.osg_test_libs, 
                                       lib_map,
                                      osg_lib_suffix=shared_lib_suffix)   
      test_env = build_env.Copy()
      test_env.Append(CPPPATH=library.test_cpppath + config_info.getIncPath(),
                      LIBPATH=library.test_libpath + [inst_paths["lib_inst_combo"]] + config_info.getLibPath(),
                      LIBS=config_info.getLibs() + library.other_test_libs)
   
      # - Build them all in the local 'test' subdir
      for s in library.test_files:
         test_env.Program(target= pj("..","test",os.path.splitext(os.path.basename(s))[0]),
                          source=s)
   
      # ---- Build Unit Tests ---- #   
      # - Build them all in the unittest directory
      if build_env["enable_unittests"] and (len(library.unittest_files) > 0):         
         unittest_env = test_env.Copy()
         unittest_env.Append(CPPPATH=unittest_inc,
                             LIBPATH=unittest_libpath,
                             LIBS=unittest_lib)
         runner_obj = unittest_env.Object(target=pj("..","unittest","run"+lib_name), source=unittest_runner)
         runner = unittest_env.Program(pj("..","unittest","run"+lib_name), 
                                       library.unittest_files + [str(runner_obj[0])])

         # Run the Test once the program is built
         def runTest(target, source, env):
            path = ""
            for i in env["LIBPATH"]:
                path += i + os.pathsep
            path = path[:-1]
            
            ev = os.environ
            if GetPlatform() == "win32":
                key = "PATH"
            else:
                key = "LD_LIBRARY_PATH"
                
            if ev.has_key(key):
                ev[key] = os.pathsep + path + ev[key]
            else:
                ev[key] = path
            
            os.spawnle(os.P_WAIT, str(target[0]), ev)

         AddPostAction(runner, runTest)

      
   # Done   
if not verbose_build:
   print ""
